buildscript {
  ext.kotlin_version = '1.3.61'
  ext.wildfly_version = '18.0.1.Final'
  ext.keycloak_version = '8.0.1'
  ext.liquibase_version = "3.8.0"

  repositories {
    jcenter()
  }

  dependencies {  
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.avast.gradle:gradle-docker-compose-plugin:0.10.7"
  }
}

apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'docker-compose'

sourceCompatibility = 11
targetCompatibility = 11

dockerCompose.isRequiredBy(test)

repositories {
  jcenter()
}

dependencies {
  implementation platform("org.wildfly:wildfly-spec-api:$wildfly_version")
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

  compileOnly 'org.jboss.spec.javax.servlet:jboss-servlet-api_4.0_spec'
  compileOnly 'org.jboss.resteasy:resteasy-jaxrs'
  compileOnly 'org.jboss.spec.javax.ws.rs:jboss-jaxrs-api_2.1_spec'
  compileOnly 'org.hibernate:hibernate-core'
  compileOnly 'org.hibernate:hibernate-entitymanager'
  compileOnly 'org.hibernate.validator:hibernate-validator'
  compileOnly "jakarta.validation:jakarta.validation-api"
  compileOnly "org.jboss.spec.javax.ejb:jboss-ejb-api_3.2_spec"
  compileOnly "jakarta.inject:jakarta.inject-api"
  compileOnly "jakarta.enterprise:jakarta.enterprise.cdi-api"
  compileOnly "jakarta.persistence:jakarta.persistence-api"
  compileOnly "org.keycloak:keycloak-core:$keycloak_version"
  compileOnly "org.keycloak:keycloak-adapter-spi:$keycloak_version"
  compileOnly "org.keycloak:keycloak-authz-client:$keycloak_version"
  compileOnly group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.1'
  compileOnly "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"

  implementation "org.liquibase:liquibase-cdi:$liquibase_version"

  testImplementation "org.apache.commons:commons-lang3"
  compileOnly "org.apache.commons:commons-lang3"
  implementation project(':api-spec')

  testImplementation 'junit:junit:4.12'
  testImplementation "fi.metatavu.jaxrs.testbuilder:jaxrs-functional-test-builder:1.0.4"
  testCompile project(':api-client')
}

task waitApiUp() {
  doFirst {
    def url = "http://localhost:1234/v1/system/ping"
    println "Waiting for ${url} ..."
    while (!doHead(url)) {
      sleep(100)
    }
    println "${url} is up!"
  }
}

boolean doHead(url) {
  def connection = new URL(url).openConnection()
  connection.requestMethod = 'GET'
  try {
    connection.responseCode == 200
  } catch (IOException error) {
    false
  }
}

test {
  useJUnit()
  maxHeapSize = '1G'
  dependsOn tasks.waitApiUp
}

dockerCompose {
  buildBeforeUp = true
  captureContainersOutput = true
}