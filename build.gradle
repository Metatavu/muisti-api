buildscript {
  ext.kotlin_version = '1.3.61'
  ext.wildfly_version = '18.0.1.Final'
  ext.keycloak_version = '8.0.1'
  ext.jsr305_version = "3.0.2"

  repositories {
    jcenter()
  }

  dependencies {  
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    classpath "com.avast.gradle:gradle-docker-compose-plugin:0.10.7"
  }
}

plugins {
  id "org.openapi.generator" version "4.2.2"
}

apply plugin: 'java-gradle-plugin'
apply plugin: 'kotlin'
apply plugin: 'war'
apply plugin: 'docker-compose'

dockerCompose.isRequiredBy(test)

repositories {
  jcenter()
}

dependencies {
  implementation platform("org.wildfly:wildfly-spec-api:$wildfly_version")

  compileOnly 'org.jboss.spec.javax.servlet:jboss-servlet-api_4.0_spec'
  compileOnly 'org.jboss.resteasy:resteasy-jaxrs'
  compileOnly 'org.jboss.spec.javax.ws.rs:jboss-jaxrs-api_2.1_spec'
  compileOnly 'org.hibernate:hibernate-core'
  compileOnly 'org.hibernate:hibernate-entitymanager'
  compileOnly 'org.hibernate.validator:hibernate-validator'
  compileOnly "jakarta.validation:jakarta.validation-api"
  compileOnly "org.jboss.spec.javax.ejb:jboss-ejb-api_3.2_spec"
  compileOnly "jakarta.inject:jakarta.inject-api"
  compileOnly "jakarta.enterprise:jakarta.enterprise.cdi-api"
  compileOnly "org.keycloak:keycloak-core:$keycloak_version"
  compileOnly "org.keycloak:keycloak-adapter-spi:$keycloak_version"

  implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
  compile "com.google.code.findbugs:jsr305:$jsr305_version"

  testImplementation "org.junit.jupiter:junit-jupiter:5.5.2"
}

tasks.test {
	useJUnitPlatform()
	testLogging {
		events("passed", "skipped", "failed")
	}
}

dockerCompose {
  buildBeforeUp = true
}

openApiGenerate {
  generatorName = "jaxrs-spec"
  inputSpec = "$rootDir/specs/swagger.yaml".toString()
  outputDir = "$rootDir/src/gen/java/main".toString()
  apiPackage = "fi.metatavu.muisti.api.spec"
  invokerPackage = "fi.metatavu.muisti.api.spec.invoker"
  modelPackage = "fi.metatavu.muisti.api.spec.model"
  configOptions = [
      dateLibrary: "java8",
      interfaceOnly: "true",
      returnResponse: "true",
      useSwaggerAnnotations: "false"
  ]
}

compileJava.dependsOn tasks.openApiGenerate