buildscript {
    ext.wildfly_version = '18.0.1.Final'

    repositories {
        jcenter()
    }
}

plugins {
    id "org.openapi.generator" version "4.2.2"
}

apply plugin: 'java'

repositories {
    jcenter()
}

dependencies {
    implementation platform("org.wildfly:wildfly-spec-api:$wildfly_version")

    compileOnly 'org.jboss.resteasy:resteasy-jaxrs'
    compileOnly 'org.jboss.spec.javax.ws.rs:jboss-jaxrs-api_2.1_spec'
    compileOnly "jakarta.validation:jakarta.validation-api"
    compileOnly "com.fasterxml.jackson.core:jackson-annotations"
    
}

openApiGenerate {
    generatorName = "jaxrs-spec"
    inputSpec = "$rootDir/muisti-api-spec/swagger.yaml".toString()
    outputDir = "$buildDir/generated".toString()
    apiPackage = "fi.metatavu.muisti.api.spec"
    invokerPackage = "fi.metatavu.muisti.api.spec.invoker"
    modelPackage = "fi.metatavu.muisti.api.spec.model"
    configOptions = [
            dateLibrary: "java8",
            interfaceOnly: "true",
            returnResponse: "true",
            useSwaggerAnnotations: "false"
    ]
}

task copyGeneratedCode(type: Copy) {
    from "$buildDir/generated/src/gen"
    into "src/main"
    dependsOn tasks.openApiGenerate
}

compileJava.dependsOn tasks.copyGeneratedCode